// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath 'com.novoda:bintray-release:0.3.4'
//        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
//        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    ARTIFACT_ID = 'UserGuideView'
    VERSION_NAME = '1.0.1'
    VERSION_CODE = 2

    DESCRIPTION = 'an userguide view'

    SITE_URL = 'https://github.com/yilylong/UserGuideView'
    GIT_URL = 'https://github.com/yilylong/UserGuideView.git'
    GROUP_NAME = 'com.zhl.userguideview'
    COMPILE_SDK = 23
    BUILD_TOOLS = '23.0.1'

    MODULE_NAME = 'Userguidelibrary'

    LICENSE = 'Apache-2.0'

    DEVELOPER_ID = 'yilylong'
    DEVELOPER_NAME = 'hailongzhao'
    DEVELOPER_EMAIL = 'yilylong@163.com'

    IS_UPLOADING = project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}
}

subprojects {
    group = GROUP_NAME
    version = VERSION_NAME

    if (IS_UPLOADING && project.name in ['Userguidelibrary']) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                println 'Overriding pom-file to make sure we can sync to maven central!'
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name "$project.name"
                        artifactId ARTIFACT_ID
                        packaging project.name == 'compiler' ? 'jar' : 'aar'
                        description DESCRIPTION
                        url SITE_URL
                        version VERSION_NAME

                        scm {
                            url GIT_URL
                            connection GIT_URL
                            developerConnection GIT_URL
                        }

                        licenses {
                            license {
                                name LICENSE
                            }
                        }

                        developers {
                            developer {
                                id DEVELOPER_ID
                                name DEVELOPER_NAME
                                email DEVELOPER_EMAIL
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}


